# Copyright (C) 2021 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//base/accessibility/frameworks/asacfwk/asacfwk.gni")
import("//build/ohos.gni")

config("accessibilityclient_private_config") {
  visibility = [ ":*" ]

  include_dirs = [
    "//base/accessibility/frameworks/asacfwk/include",
    "//base/accessibility/util/include",
    "//base/accessibility/common/log/include",
    "//base/accessibility/dummyData",
    "//foundation/aafwk/standard/interfaces/innerkits/base/include",
    "//foundation/appexecfwk/standard/interfaces/innerkits/appexecfwk_base/include",
  ]

  defines = [
    "AAMS_LOG_TAG = \"accessibility_aakit\"",
    "AAMS_LOG_DOMAIN = 0xD005202",
  ]

  if (target_cpu == "arm") {
    cflags = [ "-DBINDER_IPC_32BIT" ]
  }
}

config("accessibilityclient_public_config") {
  include_dirs = [ "include" ]
}

ohos_shared_library("accessibilityclient") {
  install_enable = true

  sources = asacfwk_files

  configs = [ ":accessibilityclient_private_config" ]

  public_configs = [ ":accessibilityclient_public_config" ]

  deps = [
    "//base/miscservices/time/services:time_service",
    "//foundation/aafwk/standard/frameworks/kits/ability/native:abilitykit_native",
    "//utils/native/base:utils",
  ]

  external_deps = [
    "ability_runtime:ability_manager",
    "ability_runtime:want",
    "appexecfwk_standard:appexecfwk_base",
    "appexecfwk_standard:appexecfwk_core",
    "hiviewdfx_hilog_native:libhilog",
    "ipc:ipc_core",
    "samgr_standard:samgr_proxy",
  ]

  subsystem_name = "barrierfree"
  part_name = "accessibility"
}
